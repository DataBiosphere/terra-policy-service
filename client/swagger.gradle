def jerseyVersion = '2.40'

dependencies {
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.2'
    implementation "org.glassfish.jersey.core:jersey-client:$jerseyVersion"
    implementation "org.glassfish.jersey.core:jersey-common:$jerseyVersion"
    implementation "org.glassfish.jersey.media:jersey-media-json-jackson:$jerseyVersion"
    implementation "org.glassfish.jersey.media:jersey-media-multipart:$jerseyVersion"
    implementation "org.glassfish.jersey.inject:jersey-hk2:$jerseyVersion"
    implementation "org.glassfish.jersey.connectors:jersey-apache-connector:$jerseyVersion"

    implementation 'io.swagger.core.v3:swagger-annotations:2.2.0'
    swaggerCodegen 'io.swagger.codegen.v3:swagger-codegen-cli:3.0.35'


    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'jakarta.ws.rs:jakarta.ws.rs-api:2.1.6'
}

def artifactGroup = "${group}.policy"

generateSwaggerCode {
    inputFile = file('../common/openapi.yml')
    language = 'java'
    library = 'jersey2'

    // For Swagger Codegen v3 on Java 16+
    // See https://github.com/swagger-api/swagger-codegen/issues/10966
    jvmArgs = ['--add-opens=java.base/java.util=ALL-UNNAMED']

    components = [
        apiDocs  : false, apiTests: false,
        modelDocs: false, modelTests: false
    ]

    templateDir = file("${projectDir}/src/main/resources/swaggercodegen")
    additionalProperties = [
        modelPackage  : "${artifactGroup}.model",
        apiPackage    : "${artifactGroup}.api",
        invokerPackage: "${artifactGroup}.client",
        dateLibrary   : 'java8'
    ]

    rawOptions = ['--ignore-file-override', "${projectDir}/.swagger-codegen-ignore"]
}

idea.module.generatedSourceDirs = [file("${generateSwaggerCode.outputDir}/src/main/java")]
sourceSets.main.java.srcDir "${generateSwaggerCode.outputDir}/src/main/java"
compileJava.dependsOn generateSwaggerCode
sourcesJar.dependsOn generateSwaggerCode
