def jerseyVersion = '3.1.2'

dependencies {
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.2'

    implementation "org.glassfish.jersey.core:jersey-client:$jerseyVersion"
    implementation "org.glassfish.jersey.media:jersey-media-json-jackson:$jerseyVersion"
    implementation "org.glassfish.jersey.media:jersey-media-multipart:$jerseyVersion"

    implementation 'jakarta.annotation:jakarta.annotation-api:2.1.1'
    implementation 'jakarta.ws.rs:jakarta.ws.rs-api:3.1.0'
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.12'
    swaggerCodegen 'io.swagger.codegen.v3:swagger-codegen-cli:3.0.45'
}

def artifactGroup = "${group}.policy"

generateSwaggerCode {
    inputFile = file('../common/openapi.yml')
    language = 'java'
    library = 'jersey3'

    components = [
        apiDocs  : false, apiTests: false,
        modelDocs: false, modelTests: false
    ]

    additionalProperties = [
        modelPackage  : "${artifactGroup}.model",
        apiPackage    : "${artifactGroup}.api",
        invokerPackage: "${artifactGroup}.client",
        dateLibrary   : 'java8',
        jakarta 	  :  true
    ]

    rawOptions = ['--ignore-file-override', "${projectDir}/.swagger-codegen-ignore"]
}

idea.module.generatedSourceDirs = [file("${generateSwaggerCode.outputDir}/src/main/java")]
sourceSets.main.java.srcDir "${generateSwaggerCode.outputDir}/src/main/java"
compileJava.dependsOn generateSwaggerCode
sourcesJar.dependsOn generateSwaggerCode
