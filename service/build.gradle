
import org.springframework.boot.gradle.plugin.SpringBootPlugin

buildscript {
    dependencies {
        // required by the liquibase plugin; see https://github.com/liquibase/liquibase-gradle-plugin/blob/master/doc/releases.md
        // must match the version of liquibase-core specified by liquibaseRuntime below
        classpath 'org.liquibase:liquibase-core:4.33.0'
    }
}

plugins {
    id 'bio.terra.policy.java-spring-app-conventions'
    id 'de.undercouch.download'
    id 'com.google.cloud.tools.jib'
    id 'org.sonarqube'
    id 'org.liquibase.gradle' version '3.0.2'
    id 'org.hidetake.swagger.generator'
    id 'io.spring.dependency-management'
    id 'com.gorylenko.gradle-git-properties' version '2.5.3'
}

apply from: 'publishing.gradle'
apply from: 'generators.gradle'

dependencyManagement {
    imports {
        mavenBom(SpringBootPlugin.BOM_COORDINATES)
    }
}

def springBootVersion = '3.5.6'

dependencies {

    implementation 'com.google.guava:guava:33.5.0-jre'
    implementation 'org.apache.taglibs:taglibs-standard-impl:1.2.5'
    implementation 'org.json:json:20250517'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.20.0'
    implementation 'com.google.oauth-client:google-oauth-client:1.39.0'
    implementation 'org.scala-lang:scala-library:2.13.17' //required by the sam-client
    // when updating terra-common-lib, also update the opentelemetry versions in Java common conventions
    implementation('bio.terra:terra-common-lib:1.1.43-SNAPSHOT') {
        exclude group: 'org.springframework.boot'
        exclude group: 'org.yaml', module: 'snakeyaml'
    }
    implementation('org.yaml:snakeyaml') {
        version {
            strictly('2.0')
        }
    }

    implementation 'org.apache.commons:commons-dbcp2:2.13.0'
    implementation 'javax.ws.rs:javax.ws.rs-api:2.1.1'
    implementation 'org.postgresql:postgresql:42.7.8'

    implementation "org.springframework.boot:spring-boot-starter-data-jdbc:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-web:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-validation:$springBootVersion"
    implementation "org.springframework.boot:spring-boot-starter-thymeleaf:$springBootVersion"
    implementation 'org.springframework.retry:spring-retry:2.0.12'
    implementation "org.springframework:spring-aop:6.2.11"
    implementation 'org.springframework:spring-aspects:6.2.11'
    implementation 'io.sentry:sentry-spring-boot-starter:8.22.0'
    implementation 'ch.qos.logback:logback-classic:1.5.19'
    implementation 'ch.qos.logback:logback-core:1.5.19'

    implementation 'org.apache.commons:commons-lang3:3.19.0'

    liquibaseRuntime 'org.liquibase:liquibase-core:4.33.0'
    liquibaseRuntime 'info.picocli:picocli:4.7.7'
    liquibaseRuntime 'org.postgresql:postgresql:42.7.8'
    liquibaseRuntime 'ch.qos.logback:logback-classic:1.5.19'
    liquibaseRuntime 'ch.qos.logback:logback-core:1.5.19'

    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:$springBootVersion"

    // Spotbugs dependencies
    compileOnly "com.github.spotbugs:spotbugs-annotations:${spotbugs.toolVersion.get()}"
    spotbugs "com.github.spotbugs:spotbugs-annotations:${spotbugs.toolVersion.get()}"
    spotbugs "com.github.spotbugs:spotbugs:${spotbugs.toolVersion.get()}"
    spotbugs 'org.slf4j:slf4j-simple:2.0.17'

    testImplementation 'org.jacoco:org.jacoco.agent:0.8.13'
    testImplementation("org.springframework.boot:spring-boot-starter-test:$springBootVersion") {
        // Fixes warning about multiple occurrences of JSONObject on the classpath
        exclude group: 'com.vaadin.external.google', module: 'android-json'
    }

    // Metrics collection
    implementation "org.springframework.boot:spring-boot-starter-actuator:$springBootVersion"
    implementation 'io.micrometer:micrometer-registry-prometheus:1.15.4'

}

test {
    useJUnitPlatform ()
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

sonarqube {
    properties {
        property 'sonar.projectName', 'terra-policy-service'
        property 'sonar.projectKey', 'terra-policy-service'
        property 'sonar.organization', 'broad-databiosphere'
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.sources', 'src/main/java,src/main/resources/templates'
    }
}

liquibase {
    activities {
        catalog {
            changelogFile 'src/main/resources/db/changelog.xml'
            url 'jdbc:postgresql://localhost:5432/policy_db'
            username 'dbuser'
            password 'dbpwd'
            logLevel 'info'
        }
    }
}
