dependencies {
	runtimeOnly 'org.webjars.npm:swagger-ui-dist:4.9.0'

	implementation 'jakarta.annotation:jakarta.annotation-api:2.1.1'
	implementation 'io.swagger.core.v3:swagger-annotations:2.2.12'
	swaggerCodegen 'io.swagger.codegen.v3:swagger-codegen-cli:3.0.45'

	// Versioned by Spring:
	implementation 'org.webjars:webjars-locator-core'
}

def artifactGroup = "${group}.policy"

generateSwaggerCode {
	inputFile = file('../common/openapi.yml')
	language = 'spring'
	components = ['models', 'apis']
	additionalProperties = [
			modelPackage     : "${artifactGroup}.generated.model",
			apiPackage       : "${artifactGroup}.generated.api",
			dateLibrary      : 'java11',
			interfaceOnly    : 'true',
			useTags          : 'true',
			modelNamePrefix  : 'Api',
			springBootVersion: dependencyManagement.managedVersions['org.springframework.boot:spring-boot'],
			jakarta 	  :  true
	]
}

String swaggerOutputSrc = "${generateSwaggerCode.outputDir}/src/main/java"

idea.module.generatedSourceDirs = [file(swaggerOutputSrc)]
sourceSets.main.java.srcDir swaggerOutputSrc
compileJava.dependsOn tasks.generateSwaggerCode

// see https://github.com/n0mer/gradle-git-properties
gitProperties {
	gitPropertiesName = 'rendered/version.properties'
	keys = []
	customProperty('tps.version.gitTag', { it.describe(tags: true) })
	customProperty('tps.version.gitHash', { it.head().abbreviatedId })
	customProperty('tps.version.github', { "https://github.com/DataBiosphere/terra-policy-service/tree/${it.describe(tags: true)}" })
	customProperty('tps.version.build', version)
}
